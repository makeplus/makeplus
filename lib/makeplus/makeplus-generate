#!/usr/bin/env perl

use strict;
use warnings;

our $VERSION = 'v0.0.1';

# use XXX;

my (
  $input_makefile,
  $output_makefile,
  $makeplus_root,
  $makefile,
  @test,
  %test,
  @stop,
  %stop,
  @cmds,
);

sub main {
  ($input_makefile, $output_makefile, $makeplus_root) = @_;

  if (not defined $ENV{MAKEPLUS_MAKEFILE} or
      $output_makefile ne $ENV{MAKEPLUS_MAKEFILE} or
      not -e $output_makefile
  ) {
    parse_makefile();

    generate_makefile();

    write_makefile();
  }

  print "$output_makefile\n";
}

my $re_rulestart = qr/^
  [\ \t]*               # Ignore leading spaces
  (?!\.[A-Z_]+)         # Skip built-in targets
  ([^\#\=\:]+?)         # Target spec
  [\ \t]*               # Possible spaces
  ::? (?!=)             # Finally : or :: (but not := or ::=)
/x;
my $re_testspec = qr/(\$[\(\{]\+\S+)/;
my $re_parse_testspec = qr/
  \$[\(\{]              # $( or ${
    (                   # Capture entire spec
      \+                # Begin with +
      (\!?)             # Not operator (optional)
      ([^\?\|\!\(\s]+)  # Test function (required)
      (?:
        \(
          (\S+?)        # Test arguments (optional)
        \)
      )?
      (?:
        \? ( [^\|\!\s]+ ) # Then target (optional)
      )?
      (?:
        \| ( [^\!\s]+? ) # Else target (optional)
      )?
      (!?)              # Must operator (optional)
    )
  [\}\)]                # } or )
/x;
my $re_parse_command = qr/^
  \$[\(\{]              # $( or ${
    (                   # Capture entire spec
      \+                # Begin with +
      ([-\w]+)          # Command name
      (?:
        \(
          (\S+)         # Command arguments (optional)
        \)
      )?
    )
  [\}\)]                # } or )
/x;

sub parse_makefile {
  open IN, $input_makefile
    or die "Can't open '$input_makefile' for input";

  while (defined(my $line = <IN>)) {
    chomp $line;

    if ($line =~ qr/$re_parse_command/) {
        add_command($1, $2, $3);
    }
    elsif ($line =~ s/$re_rulestart//) {

      my $rulespec = $1;

      while ($line =~ s/\\$//) {
        defined(my $next = <IN>) or last;
        chomp $next;
        $line .= $next;
      }
      while ($line =~ s/^.*?$re_testspec//) {
        add_testspec($1);
      }
    }
  }
}

sub add_command {
  my ($spec, $cmd, $args) = @_;

  $args = parse_args($args);

  push @cmds, [$spec, $cmd, $args];
}

sub add_testspec {
  my ($spec) = @_;


  my ($var, $not, $test, $args, $then, $else, $must) =
    ($spec =~ $re_parse_testspec)
      or die "Invalid makeplus test: '$spec'";

  my $val = {
    not  => $not || '',
    test => $test || die,
    args => parse_args($args),
    then => $then || '',
    else => $else || '',
    must => $must || '',
  };

  if ($must and not $stop{$test}) {
    $stop{$test} = 1;
    push @stop, [$test, $not];
  }

  push @test, $var unless defined $test{$var};
  $test{$var} = $val;
}

sub parse_args {
  my ($args) = @_;

  return '' unless length $args;

  my @args;
  while ($args =~ s/^('[^']*?'|"[^"]*?"|.+?),//) {
    push @args, $1;
    $args[-1] =~ s/%(\w+)/\$($1)/g;
  }
  push @args, $args;
  $args[-1] =~ s/%(\w+)/\$($1)/g;

  return join ' ', @args;
}

sub generate_makefile {
  $makefile = <<"...";
# MakePlus ($VERSION) generated Makefile:
${\ format_commands()}
${\ format_prerequisites()}
${\ format_stops()}
${\ format_rules()}
# Reset the default goal:
.DEFAULT_GOAL :=
...
}

sub format_commands {
  return '' unless @cmds;

  my $o = "\n# MakePlus special commands\n";

  for my $val (@cmds) {
    my ($var, $cmd, $args) = @$val;
    $o .= "$var = \$(call MAKEPLUS_CMD,$cmd,$args)\n";
  }

  $o .= <<'...';

# Macro to call a MakePlus special command:
define MAKEPLUS_CMD
$(eval makeplus_out = $(shell $(MAKEPLUS_LIB)/$1 $2)) \
$(if $(makeplus_out),$(error $(makeplus_out)))
endef
...

  return $o;
}

sub format_prerequisites {
  return '' unless @test;

  my $o = "\n# MakePlus prerequisites:\n";

  for my $var (@test) {
    my ($not, $test, $args, $then, $else, $must) =
      @{$test{$var}}{qw(not test args then else must)};
    $then =~ s/^\%(.*)/\$($1)/;
    $else =~ s/^\%(.*)/\$($1)/;
    $o .=
      "$var = \$(call MAKEPLUS_CHECK,$test,$args,$then,$else,$not,$must)\n";
  }

  $o .= <<'...';

# Macro to call MakePlus prerequisite check function:
makeplus_check = $(shell (rc=0; \
  out=$$("$(MAKEPLUS_LIB)/makeplus-check" \
         "$(MAKEPLUS_PATH)" '$(value makeplus_cmd)' \
	 "$2" "$3" "$4" "$5" "$6") \
  || rc=$$?; echo "$$rc $$out"))

define MAKEPLUS_CHECK
$(eval makeplus_cmd = $(if $(value $1),\$(value $1),$1)) \
$(eval makeplus_out = $(makeplus_check)) \
$(eval makeplus_rc = $(firstword $(makeplus_out))) \
$(eval makeplus_text = $(wordlist 2,99999,$(makeplus_out))) \
$(if $(filter $(makeplus_rc),0),$(makeplus_text),\
$(if $(filter $(makeplus_rc),2),makeplus-stop-$1))
endef

# MakePlus stop rules:
makeplus-stop-<ref-name>:
	@echo "STOP - $(stop-false)"; exit 1
...

  return $o;
}

sub format_stops {
  return '' unless @stop;

  my $o = "\n# MakePlus STOP rules:\n";

  for my $stop (@stop) {
    my ($test, $not) = @$stop;
    $o .= <<"...";

makeplus-stop-$test:
	\@echo "STOP: prerequisite '+$not$test' must be true" >&2
	\@exit 1
...

  }

  return $o;
}

sub format_rules {
  return <<"...";
# MakePlus special rules:
+makeplus:
	\@echo "MakePlus $VERSION"

...
}

sub write_makefile {
  open OUT, '>', $output_makefile
    or die "Can't open '$output_makefile' for output";

  print OUT $makefile;

  close OUT;

  print STDERR "MakePlus: $output_makefile\n"
    if $ENV{MAKEPLUS_DEBUG};
}

main(@ARGV);
